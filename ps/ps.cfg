//  cube 2 sauerbraten: particle screen
//  version 0.1
//  by micai

//  "particle screen" is a coop edit script which uses particles as pixels to create dynamic screens within maps
//  each particle entity used by the screen is assigned a unique number that allows identifying its coordinates on the screen
//  the coordinates are stored in entities' 5th attributes, so it's recommended to use particles which appearances are not affected by it
//  max number of pixels = 32767 (max 1:1 resolution = 181x181)
//  this value can be increased by using negative numbers but i don't really think there's a point in doing that now

//  usage notes: 
//  - when running the script for the first time use the "_ps_config_defaults" command to define variables used by it
//  - the "autopixel" tool requires particles to be placed from left to right starting at the top of the screen
//    after placing the particles select them and enter the command to create an usable screen
//  - you must specify the height of your screen in pixels for the scripts to work correctly

_ps_config_defaults = [
	_ps_screen_attr1 = *
	_ps_screen_attr2 = *
	_ps_screen_attr3 = *
	_ps_screen_colorattr = 2
	_ps_screen_height = 16
	_ps_screen_defaultcolor = 0x001
	_ps_screen_disabledent = box
]

_ps_config_resetkeys = [
	_ps_keys_up = ""
	_ps_keys_down = ""
	_ps_keys_left = ""
	_ps_keys_right = ""
	_ps_keys_f1 = ""
	_ps_keys_f2 = ""
	_ps_keys_f3 = ""
	_ps_keys_f4 = ""
	_ps_keys_enter = ""
	_ps_keys_alt = ""
]

// - - dev commands - - //
_ps_dev_fullgradient = [
	entfind particles $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * *
	_ps_tempvar_fullgradient_i = 1
	entloop [
		entattr $_ps_screen_colorattr (* $arg1 $_ps_tempvar_fullgradient_i)
		_ps_tempvar_fullgradient_i = (+ 1 $_ps_tempvar_fullgradient_i)
	]
	cancelsel
]

// - - screen building commands - - //
_ps_sb_autopixel = [
	_ps_tempvar_autopixel_i = 0
	entloop [
		entattr 4 $_ps_tempvar_autopixel_i
		_ps_tempvar_autopixel_i = (+ 1 $_ps_tempvar_autopixel_i)
	]
	cancelsel
]

// - - basic drawing commands - - //
_ps_off = -2147483648

_ps_px = [
	entfind particles $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * (+ (* $arg2 $_ps_screen_height) $arg1)
	entfind $_ps_screen_disabledent $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * (+ (* $arg2 $_ps_screen_height) $arg1)
	if (= $arg3 -2147483648) [
		enttype $_ps_screen_disabledent
		echo "px off"
	] [
		echo "px normal"
		enttype particles
		entattr $_ps_screen_colorattr $arg3
	]
	cancelsel
]

_ps_fill = [
	entfind particles $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * *
	entfind $_ps_screen_disabledent $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * *
	echo $arg1
	if (= $arg1 -2147483648) [
		enttype $_ps_screen_disabledent
		echo "fill off"
	] [
		enttype particles
		entattr $_ps_screen_colorattr $arg1
		echo "fill normal"
	]
	cancelsel
]

_ps_rect = [
	loop _ps_tempvar_rect_x $arg3 [
		loop _ps_tempvar_rect_y $arg4 [
			_ps_px (+ $_ps_tempvar_rect_x $arg1) (+ $_ps_tempvar_rect_y $arg2) $arg5
		]
	]
]

_ps_rep = [
	if (= $arg1 -2147483648) [
		entfind $_ps_screen_disabledent $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 * *
	]	[
		if (= $_ps_screen_colorattr 2) [
			entfind particles $_ps_screen_attr1 $_ps_screen_attr2 $arg1 * *
		] 	[
			entfind particles $_ps_screen_attr1 $_ps_screen_attr2 $_ps_screen_attr3 $arg1 *
		]
	]
	enttype particles
	entattr $_ps_screen_colorattr $arg2
	cancelsel
]

_ps_cl = [
	_ps_px $arg1 $arg2 $_ps_screen_defaultcolor
]

_ps_cls = [
	_ps_fill $_ps_screen_defaultcolor
]

_ps_clc = [
	_ps_rep $arg1 $_ps_screen_defaultcolor
]

_ps_clrect = [
	_ps_rect $arg1 $arg2 $arg3 $arg4 $_ps_screen_defaultcolor
]

_ps_clrep = [
	_ps_rep $_ps_screen_defaultcolor $arg1
]

// - - console related stuff - - //
_ps_con_echo = [
	echo "^f4ps |^f7" $arg1
]

// - - GUI related stuff - - //
_ps_gui_embed_controller = [
	guistayopen [
		guilist [ guitext "      " 0; guibar; guibutton " UP " "_ps_keys_up" 0; guibar]
		guilist [ guibar; guibutton "LEFT" "_ps_keys_left" 0; guibar; guibutton  "DOWN" "_ps_keys_down" 0; guibar; guibutton "RIGHT" "_ps_keys_right" 0; guibar]
		guibar
		guilist [ guitext "  " 0; guibar; guibutton "^f0F1" "_ps_keys_f1" 0; guibar; guibutton  "^f3F2" "_ps_keys_f2" 0; guibar; guibutton "^f1F3" "_ps_keys_f3" 0; guibar; guibutton "^f2F4" "_ps_keys_f4" 0; guibar]
		guibar
		guilist [ guibar; guibutton "^f6ENTER" "_ps_keys_enter" 0; guibar; guibutton  "^f5ALT" "_ps_keys_alt" 0; guibar; guibutton  "^f4CLOSE" "cleargui" 0; guibar]
	]
]

newgui _ps_gui_devtest1 [ 
	guititle "screen testing"
	guilist [ guibutton "fill screen " "_ps_fill $_ps_gui_devtest1_fsc"; guifield _ps_gui_devtest1_fsc 12 ]
	guibutton "clear screen" "_ps_cls"
	guibar
	guibutton "cancel" "cleargui" "exit"
	
	guitab "screen"
	guititle "screen configuration"
	guilist [ guitext "screen height: "; guifield _ps_screen_height 6 ]
	guilist [ guitext "default color: "; guifield _ps_screen_defaultcolor 12 ]
	guilist [ guitext "color attribute number: "; guifield _ps_screen_colorattr  2 ]
	guititle "screen finder"
	guilist [ guitext "particle attribute 1: "; guifield _ps_screen_attr1 2 ]
	guilist [ guitext "particle attribute 2: "; guifield _ps_screen_attr2 2 ]
	guilist [ guitext "particle attribute 3: "; guifield _ps_screen_attr3 2 ]
	guibar
	guibutton "cancel" "cleargui" "exit"
	
	guitab "controller"
	guititle "controller"
	guibar
	_ps_gui_embed_controller
	guibar
	guibar
	guibutton "standalone controller" "showgui _ps_gui_controller" "menu"
	guibutton "cancel" "cleargui" "exit"
	
] devtest

newgui _ps_gui_controller [
	_ps_gui_embed_controller
] 0

newgui _ps_gui_config [ 
	
	guibar
	guibutton "close" "cleargui" "exit"
] "ParticleScreen"

// - - Init - - //
// first run config
if (= $_ps_config_installed 0) [
	_ps_con_echo "Running ParticleScreen for the first time. Defining aliases."
	_ps_config_installed = 1
	_ps_config_defaults
	_ps_config_resetkeys
	_ps_con_echo "Aliases defined."
] [
	
]

_ps_con_echo "ParticleScreen loaded."