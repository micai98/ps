//ps_snake - by micai. created to test particle screen's interactivity
//init
clearsleep
_ps_cls
_tg_last = 0

_tg_defaults = [
	_tg_scrw = 16
	_tg_scrh = 16

	_tg_tailc = 0x060
	_tg_playerc = 0x0F2
	_tg_fruitc = 0xF00
	_tg_frameinterval = 125
]

//load defaults when running for the first time
if (= $_tg_scrw 0) [
	_tg_defaults
	_tg_best = 0
] 

_tg_init = [
	_tg_canmove = 0
	_tg_playerx = (div $_tg_scrw 2)
	_tg_playery = (div $_tg_scrh 2)
	_tg_playerhspd = 0
	_tg_playervspd = 0

	_tg_tailx = 0
	_tg_taily = 0
	
	_tg_taill = 0
	_tg_fruitx = (rnd (- $_tg_scrw 1))
	_tg_fruity = (rnd (- $_tg_scrh 1))
]

//controls
_ps_keys_up = [ 
	_tg_playerhspd = 0
	_tg_playervspd = -1
]
_ps_keys_down = [ 
	_tg_playerhspd = 0
	_tg_playervspd = 1
]
_ps_keys_left = [ 
	_tg_playerhspd = -1
	_tg_playervspd = 0
]
_ps_keys_right = [ 
	_tg_playerhspd = 1
	_tg_playervspd = 0
]

//main game loop
_tg_update = [
	if (&& (= $_tg_playerx $_tg_fruitx) (= $_tg_playery $_tg_fruity)) [ // collecting the fruit
		_tg_taill = (+ $_tg_taill 1)
		_tg_fruitx = (rnd (- $_tg_scrw 1))
		_tg_fruity = (rnd (- $_tg_scrh 1))
		_ps_con_echo ( result ( format "Score: ^f6%1" $_tg_taill ) )
	]
	
	_tg_tailx = (listsplice $_tg_tailx $_tg_playerx (listlen $_tg_tailx))					// new tail pixel on player's previous position (x)
	if (> (listlen $_tg_tailx) $_tg_taill) [ _tg_tailx = (listsplice $_tg_tailx "" 0 1) ]	// keep the correct tail length (x)
	_tg_taily = (listsplice $_tg_taily $_tg_playery (listlen $_tg_taily))					// new tail pixel on player's previous position (y)
	if (> (listlen $_tg_taily) $_tg_taill) [ _tg_taily = (listsplice $_tg_taily "" 0 1) ]	// keep the correct tail length (y)
	
	if ( $_tg_canmove ) [
		_tg_playerx = (+ $_tg_playerx $_tg_playerhspd) // move the snake
		_tg_playery = (+ $_tg_playery $_tg_playervspd) // based on its speed
	]
	
	if (> $_tg_playerx (- $_tg_scrw 1)) [ _tg_playerx = 0 ] //
	if (< $_tg_playerx 0) [ _tg_playerx = (- $_tg_scrw 1) ] // wrap the snake
	if (> $_tg_playery (- $_tg_scrh 1)) [ _tg_playery = 0 ] // around the screen
	if (< $_tg_playery 0) [ _tg_playery = (- $_tg_scrh 1) ] //
	
	_ps_cls // clear the screen
	loop _tg_i $_tg_taill [ // tail
		if (&& (= (at $_tg_tailx $_tg_i) $_tg_playerx) (= (at $_tg_taily $_tg_i) $_tg_playery)) [ // check if the snake bit its tail
			_ps_con_echo "You lost."
			if (> $_tg_taill $_tg_best) [ // update high score
				_ps_con_echo "New high score!"
				_tg_best = $_tg_taill
			]
			_tg_last = $_tg_taill // save the last score to display in main menu
			_tg_init // reset game
			showgui _tg_gui_main // return to main menu
		]
		_ps_px (at $_tg_tailx $_tg_i) (at $_tg_taily $_tg_i) $_tg_tailc // draw the tail
	]

	_ps_px $_tg_playerx $_tg_playery  $_tg_playerc // draw the snake's head
	_ps_px $_tg_fruitx $_tg_fruity  $_tg_fruitc // draw the fruit
	
	sleep $_tg_frameinterval [ _tg_update ] // loop the game
]

//configuration menu
newgui _tg_gui_main [
	if (> $_tg_last 0) [ guitext ( result ( format "last score: %1" $_tg_last ) ) "coins" ]
	guitext ( result ( format "high score: %1" $_tg_best ) ) "coins"
	guibar
	guibutton "start game" [ _tg_canmove = 1 ]"spectator"
	guibutton "quit game" [ _tg_update = 0 ] "exit"
	
	guitab "configuration"
	guilist [ guitext "frame interval: "; guifield _tg_frameinterval 4 ]
	guilist [ guitext "game width: "; guifield _tg_scrw 4 ]
	guilist [ guitext "game height: "; guifield _tg_scrh 4 ]
	guilist [ guitext "player color: "; guifield _tg_playerc 6 ]
	guilist [ guitext "tail color: "; guifield _tg_tailc 6 ]
	guilist [ guitext "fruit color: "; guifield _tg_fruitc 6 ]
	guibar
	guilist [ guitext "screen height: "; guifield _ps_screen_height 6 ]
	guistayopen [
		guibutton "restore defaults" [ _tg_defaults ]
		guibutton "clear high score" [ _tg_best = 0 ]
	]
	guitab "info"
	guititle "ps_snake by micai"
	guititle "github.com/micai98/ps"
	guibar
	guitext "controls: " 0
	guitext "use /showgui _ps_gui_controller" 0
	guitext "or editbind following commands:" 0
	guitext "_ps_keys_up    _ps_keys_down" 0
	guitext "_ps_keys_left   _ps_keys_right" 0
	
] "snake"

//start the main loop
_ps_con_echo "Snake test. "
_tg_init;
_tg_update
showgui _tg_gui_main